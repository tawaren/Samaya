plugins {
    id 'java'
    id 'scala'
    id 'antlr'
    id 'idea'
    id 'application'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group 'Samaya'
version '1.0-SNAPSHOT'

sourceCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

sourceSets {
    main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.10'
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.plokhotnyuk.jsoniter-scala:jsoniter-scala-core_2.13:2.21.3'
    compileOnly 'com.github.plokhotnyuk.jsoniter-scala:jsoniter-scala-macros_2.13:2.21.3'

    antlr "org.antlr:antlr4:4.12.0"
    implementation 'org.antlr:antlr4-runtime:4.12.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'ky.korins:blake3_2.11:2.9.0'
}

generateGrammarSource {
    arguments += ['-visitor']
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}

mainClassName = 'samaya.Main'
