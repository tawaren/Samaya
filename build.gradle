plugins {
    id 'java'
    id 'scala'
    id 'antlr'
    id 'application'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

group 'Samaya'
version '1.0-SNAPSHOT'

sourceCompatibility = 21

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

sourceSets {
    main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.10'
    testImplementation 'junit:junit:5.10.2'
    implementation 'com.github.plokhotnyuk.jsoniter-scala:jsoniter-scala-core_2.13:2.21.3'
    compileOnly 'com.github.plokhotnyuk.jsoniter-scala:jsoniter-scala-macros_2.13:2.21.3'

    antlr "org.antlr:antlr4:4.13.1"
    implementation 'org.antlr:antlr4-runtime:4.13.1'
    implementation 'commons-codec:commons-codec:1.16.1'
    implementation 'ky.korins:blake3_2.13:2.9.0'

    implementation 'io.circe:circe-core_2.13:0.14.6'
    implementation 'io.circe:circe-generic_2.13:0.14.6'
    implementation 'io.circe:circe-parser_2.13:0.14.6'

    implementation('net.java.truevfs:truevfs-profile-default:0.14.0') {
        exclude(group : 'net.java.truevfs', module : 'truevfs-driver-zip-raes')
    }

    implementation 'net.java.truevfs:truevfs-driver-jar:0.14.0'
    //implementation 'net.java.truevfs:truevfs-driver-http:0.14.0'

    implementation 'org.slf4j:slf4j-api:2.0.12'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.12'

}


shadowJar {
    mergeServiceFiles()
}

generateGrammarSource {
    arguments += ['-visitor']
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}

mainClassName = 'samaya.Main'
