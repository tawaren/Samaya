plugins {
    id 'java'
    id 'scala'
    id 'antlr'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group 'Samaya'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

sourceSets {
    main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.12.10'
    testImplementation 'junit:junit:4.12'
    compile group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-macros_2.12', version: '0.49.1'
    compile group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-core_2.12', version: '0.49.1'
    antlr "org.antlr:antlr4:4.7.1"
    compile "org.antlr:antlr4-runtime:4.7.1"
    compile 'commons-codec:commons-codec:1.14'
    compile 'io.github.rctcwyvrn:blake3:1.3'
}

generateGrammarSource {
    arguments += ['-visitor']
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}
